# Maintainer: k1f0 <generic at k1f0.mozmail.com>
# Contributor: Firegem <mrfiregem [at] protonmail [dot] ch>
pkgname=wpaperd
pkgver=1.0.1
pkgrel=1
pkgdesc='Wallpaper daemon for Wayland.'
arch=('x86_64')
url='https://github.com/danyspin97/wpaperd'
license=('GPL3')
depends=('libxkbcommon')
makedepends=('cargo' 'scdoc')
provides=("${pkgname}")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('4ed30c90dc14fa629ac977ace3ca4a146a33d85f73d2d49915643fbb9ea53ab9')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cargo fetch --target "${CARCH}-unknown-linux-gnu"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release
  scdoc <man/wpaperd-output.5.scd >man/wpaperd-output.5
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # executables
  install -Dm0755 -t "${pkgdir}/usr/bin/" "target/release/wpaperd"
  install -Dm0755 -t "${pkgdir}/usr/bin/" "target/release/wpaperctl"
  # man pages
  install -Dm0644 -t "${pkgdir}/usr/share/man/man5" man/wpaperd-output.5
  # completion
  install -Dm0644 target/release/completions/${pkgname}.bash "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  install -Dm0644 -t "${pkgdir}/usr/share/fish/vendor_completions.d" target/release/completions/${pkgname}.fish
  install -Dm0644 -t "${pkgdir}/usr/share/zsh/site-functions" target/release/completions/_${pkgname}
  install -Dm0644 -t "${pkgdir}/usr/share/elvish/completions" target/release/completions/${pkgname}.elv
}

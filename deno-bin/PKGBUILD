# Maintainer: k1f0 <archlinux at k1f0.mozmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Metal A-wing <1 at 233 dot email>

pkgname=deno-bin
pkgver=1.43.3
pkgrel=1
pkgdesc="A secure runtime for JavaScript and TypeScript"
arch=('x86_64')
url='https://deno.land'
license=('MIT')
options=('!lto')
depends=('gcc-libs')
makedepends=('unzip')
provides=("${pkgname%-bin}")
conflicts=("${pkgname%-bin}")
source=("${pkgname}-${pkgver}::https://github.com/denoland/deno/releases/download/v${pkgver}/deno-x86_64-unknown-linux-gnu.zip")
b2sums=('3b99682dcc47ad7bfb51062764f1dab4c9536f9d33f1a3818be4f15dcbff05b5664d59404ed33cfa53f287c650f3959480ea5baee337e5102ff9866d18817e5b')

package() {
  install -Dm755 ./deno "$pkgdir"/usr/bin/deno
  install -dm755 "$pkgdir"/usr/share/bash-completion/completions
  ./deno completions bash > "$pkgdir"/usr/share/bash-completion/completions/deno
  install -dm755 "$pkgdir"/usr/share/zsh/site-functions
  ./deno completions zsh > "$pkgdir"/usr/share/zsh/site-functions/_deno
  install -dm755 "$pkgdir"/usr/share/fish/vendor_functions.d
  ./deno completions fish > "$pkgdir"/usr/share/fish/vendor_functions.d/deno.fish
}

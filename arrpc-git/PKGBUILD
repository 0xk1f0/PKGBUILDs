# Maintainer: k1f0 <generic at k1f0.mozmail.com>
# Contributor: Frederik “Freso” S. Olesen <archlinux@freso.dk>
# Contributor: Scarlett Veldt <scarlett AT enby DOT site>
pkgname=arrpc-git
pkgver=r120.c29adc0
pkgrel=1
pkgdesc="Open Discord RPC server for atypical setups"
arch=('x86_64' 'x86_64_v3')
url="https://arrpc.openasar.dev/"
license=('MIT')
depends=("glibc" "gcc-libs" "nodejs")
makedepends=("npm")
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
_url="https://github.com/Justsnoopy30/arrpc.git#branch=linux-detection-fixes"
source=("${pkgname}::git+${_url}"
    	"arrpc.service")
sha256sums=('SKIP'
    		'dccc409ef768766d8d4141f8d89366e0bbecd30d9b08d4d31856b01f180c4005')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname}"
  node update_db.js
  npm install
  npm pack
}

package() {
  cd "${srcdir}/${pkgname}"
  VERSION=$(grep version package.json | cut -d ":" -f 2 | tr -d ' ,"')
  npm install --cache "${srcdir}/npm-cache" \
    -g --prefix "${pkgdir}/usr" \
    "${pkgname%-git}-${VERSION}.tgz"

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 ../arrpc.service "${pkgdir}/usr/lib/systemd/user/arrpc.service"
}
